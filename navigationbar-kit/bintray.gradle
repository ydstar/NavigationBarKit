apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
//源码站点链接
def siteUrl = 'https://github.com/ydstar/NavigationBarKit'
//git 链接
def gitUrl = 'https://github.com/ydstar/NavigationBarKit.git'
//实例 implementation 'com.android.ydkit:navigationbar-kit:1.0.0' :group+name+version
//仓库组
group = 'com.android.ydkit'
//版本
version = '1.0.0'
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'navigationbar-kit'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
//编译 Javadoc
task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
//编译 xxx-javadoc.jar
task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}
//编译 xxx-sources.jar
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
//读取配置的账户与apiKey
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        version {
            name = '1.0.0'
            desc = '顶部导航条组件'
        }
        repo = 'NavigationBarKitRepo' //repository 仓库名称,要能对应上
        name = 'navigationbarkit'//仓库中包的名称
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        publish = true
    }
}